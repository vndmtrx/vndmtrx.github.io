---
title: Aprendendo Elixir - Estrutura Modular com Umbrella
date: 2025-04-20 15:46:00
permalink: /posts/elixir-umbrella/
tags: Programa√ß√£o, Programa√ß√£o Funcional, Elixir, Umbrella, Modulariza√ß√£o, Testes
---

Neste segundo post da s√©rie *Aprendendo Elixir*, vamos explorar como organizar projetos maiores usando **arquitetura modular** com uma estrutura de guarda-chuva (chamado *Umbrella* pelo Elixir). A ideia √© dividir o sistema em m√∫ltiplas aplica√ß√µes menores e coesas, que podem ser desenvolvidas, testadas e integradas dentro de uma aplica√ß√£o principal. Esse modelo segue o princ√≠pio de separa√ß√£o de responsabilidades, facilitando a manuten√ß√£o e o crescimento do c√≥digo. 

Os exemplos desse post est√£o em [02-umbrella](https://github.com/vndmtrx/estudo_elixir/tree/main/02-umbrella), e os demais estar√£o organizados no [reposit√≥rio principal do projeto](https://github.com/vndmtrx/estudo_elixir).

Nosso projeto de exemplo √© um **conversor unificado** com tr√™s m√≥dulos:

- `conversor_temperatura` ‚Äì convers√£o entre Celsius e Fahrenheit
- `conversor_distancia` ‚Äì convers√£o entre metros e p√©s
- `main` ‚Äì aplica√ß√£o principal que orquestra as convers√µes com intera√ß√£o do usu√°rio

## Por que usar um projeto Guarda-Chuva?

Projetos Guarda-Chuva s√£o ideais quando queremos:

- **Modularizar funcionalidades** de forma clara e reaproveit√°vel
- **Separar dom√≠nios** de forma independente (ex: API, banco, workers)
- Facilitar **testes e manuten√ß√£o** em projetos maiores
- Gerenciar **depend√™ncias locais** entre m√≥dulos sem necessidade de publicar pacotes

Cada m√≥dulo tem seu pr√≥prio ciclo de vida e testes, mas todos podem ser carregados e orquestrados a partir do projeto raiz. O Elixir lida muito bem com esse modelo gra√ßas √† sua estrutura de aplica√ß√µes OTP, onde cada subprojeto pode ter seu ciclo de vida pr√≥prio.

## Criando o projeto guarda-chuva

Crie o projeto principal com:

```bash
mix new conversor --umbrella
cd conversor
```

O comando `--umbrella` cria uma estrutura inicial com a pasta `apps/` onde viver√£o as subaplica√ß√µes.

> ‚ö†Ô∏è **Aviso**: A partir daqui, sempre que falarmos da **raiz** do projeto, estamos considerando esta pasta inicial criada com o comando acima.

Agora dentro do diret√≥rio `apps`, criamos os tr√™s subprojetos:

```bash
cd apps
mix new conversor_distancia
mix new conversor_temperatura
mix new main
```

## Estrutura do projeto

A estrutura do projeto ap√≥s esses comandos ser√°:

```
conversor
‚îú‚îÄ‚îÄ apps
‚îÇ¬†¬† ‚îú‚îÄ‚îÄ conversor_distancia
‚îÇ¬†¬† ‚îÇ¬†¬† ‚îú‚îÄ‚îÄ lib
‚îÇ¬†¬† ‚îÇ¬†¬† ‚îÇ¬†¬† ‚îî‚îÄ‚îÄ conversor_distancia.ex
‚îÇ¬†¬† ‚îÇ¬†¬† ‚îî‚îÄ‚îÄ test
‚îÇ¬†¬† ‚îÇ¬†¬†     ‚îî‚îÄ‚îÄ conversor_distancia_test.exs
‚îÇ¬†¬† ‚îú‚îÄ‚îÄ conversor_temperatura
‚îÇ¬†¬† ‚îÇ¬†¬† ‚îú‚îÄ‚îÄ lib
‚îÇ¬†¬† ‚îÇ¬†¬† ‚îÇ¬†¬† ‚îî‚îÄ‚îÄ conversor_temperatura.ex
‚îÇ¬†¬† ‚îÇ¬†¬† ‚îî‚îÄ‚îÄ test
‚îÇ¬†¬† ‚îÇ¬†¬†     ‚îî‚îÄ‚îÄ conversor_temperatura_test.exs
‚îÇ¬†¬† ‚îî‚îÄ‚îÄ main
‚îÇ¬†¬†     ‚îú‚îÄ‚îÄ lib
‚îÇ¬†¬†     ‚îÇ¬†¬† ‚îî‚îÄ‚îÄ main.ex
‚îÇ¬†¬†     ‚îú‚îÄ‚îÄ mix.exs
‚îÇ¬†¬†     ‚îî‚îÄ‚îÄ test
‚îÇ¬†¬†         ‚îî‚îÄ‚îÄ main_test.exs
‚îî‚îÄ‚îÄ mix.exs
```

> ‚ö†Ô∏è **Aviso**: Esta n√£o √© a estrutura completa gerada pelos comandos, mas somente os arquivos que iremos nos preocupar neste momento.

O arquivo `mix.exs` da raiz define que o projeto √© do tipo umbrella por conta da op√ß√£o `apps_path: "apps"`, fazendo a centraliza√ß√£o da orquestra√ß√£o do projeto.

## Implementando os m√≥dulos

### `Conversor.Distancia`

Este m√≥dulo de exemplo lida com convers√µes entre metros e p√©s.

**apps/conversor_distancia/lib/conversor_distancia.ex:**

```elixir
defmodule Conversor.Distancia do
  @moduledoc """
  Convers√£o entre metros e p√©s.
  """

  @doc """
  Converte metros para p√©s.

      iex> Conversor.Distancia.m_para_ft(1) |> Float.round(4)
      3.2808
  """
  def m_para_ft(m) when is_number(m), do: m * 3.28084

  @doc """
  Converte p√©s para metros.

      iex> Conversor.Distancia.ft_para_m(1) |> Float.round(4)
      0.3048
  """
  def ft_para_m(ft) when is_number(ft), do: ft / 3.28084
end
```

### `Conversor.Temperatura`

Este m√≥dulo de exemplo trata de convers√£o entre escalas de temperatura:

**apps/conversor_temperatura/lib/conversor_temperatura.ex:**

```elixir
defmodule Conversor.Temperatura do
  @moduledoc """
  Convers√£o entre Celsius e Fahrenheit.
  """

  @doc """
  Converte Celsius para Fahrenheit.

      iex> Conversor.Temperatura.c_para_f(0)
      32.0
  """
  def c_para_f(c) when is_number(c), do: (c * 1.8) + 32

  @doc """
  Converte Fahrenheit para Celsius.

      iex> Conversor.Temperatura.f_para_c(212)
      100.0
  """
  def f_para_c(f) when is_number(f), do: (f - 32) / 1.8
end
```

> üí° Uma coisa interessante sobre m√≥dulos no Elixir √© que eles costumam usar nomes com pontos (`.`) para formar uma estrutura mais organizada, como `Conversor.Temperatura`. Isso ajuda a deixar o c√≥digo mais leg√≠vel e bem dividido, mostrando claramente a que parte do sistema cada m√≥dulo pertence. √â uma conven√ß√£o comum na linguagem, que facilita entender a fun√ß√£o de cada m√≥dulo s√≥ pelo nome, al√©m de evitar confus√£o com outros m√≥dulos parecidos.

## M√≥dulo integrador: `Main`

Para usar nossos m√≥dulos, iremos implementar um m√≥dulo para chamarmos direto do terminal. Pela simplicidade, vamos implementar uma simples aplica√ß√£o de terminal com captura de input.

N√£o √© para ser algo bonito, √© s√≥ para mostrar como podemos rodar nosso projeto do terminal, mas serve facilmente para entender como funciona o ponto de entrada para nossa aplica√ß√£o.

No `mix.exs` do `main`, declaramos depend√™ncias para os outros dois m√≥dulos:

**apps/main/mix.exs:**

```elixir
defp deps do
  [
    {:conversor_temperatura, in_umbrella: true},
    {:conversor_distancia, in_umbrella: true}
  ]
end
```

> üí° Em projetos do tipo *umbrella*, cada app √© isolado em sua pr√≥pria pasta, mas todos compartilham o mesmo ambiente de execu√ß√£o, o que permite que m√≥dulos definidos em um app sejam utilizados em outro. Para isso, basta declarar a depend√™ncia no `mix.exs` do app que ir√° consumir (com `{:nome_do_app, in_umbrella: true}`), como fizemos em `main`, e os m√≥dulos ficam dispon√≠veis automaticamente para uso, sem necessidade de configura√ß√£o adicional. Isso torna a comunica√ß√£o entre os apps simples e direta, mantendo a modulariza√ß√£o do projeto.

O c√≥digo interativo:

**apps/main/lib/main.ex:**

```elixir
defmodule Main do
  def main do
    IO.puts("Digite uma temperatura em Celsius:")
    celsius = get_float_input()
    fahrenheit = Conversor.Temperatura.c_para_f(celsius)
    IO.puts("Em Fahrenheit: #{fahrenheit}")

    IO.puts("Digite uma dist√¢ncia em metros:")
    metros = get_float_input()
    ft = Conversor.Distancia.m_para_ft(metros)
    IO.puts("Em p√©s: #{Float.round(ft, 4)}")
  end

  defp get_float_input do
    input = IO.gets("> ") |> String.trim()
    case Float.parse(input) do
      {valor, ""} -> valor
      _ ->
        IO.puts("Entrada inv√°lida, tente novamente.")
        get_float_input()
    end
  end
end
```

> ‚ö†Ô∏è **Aviso**: Em projetos futuros n√£o iremos usar esse modo de rodar o projeto, ele serve s√≥ de exemplo para podermos interagir com o projeto de forma *standalone*.

## Compila√ß√£o e execu√ß√£o standalone

Para compilar todo o projeto n√£o t√™m muito segredo:

```bash
mix compile
```

Isso ir√° criar uma pasta `_build` na raiz do nosso projeto. Essa pasta √© sempre gerada automaticamente, ent√£o pode ser descartada do gerenciador de vers√µes em uso.

Para rodar nosso projeto de forma *standalone*, temos tr√™s formas diferentes de rodar. Vamos passar por cada uma abaixo:

### Usando mix run

> ‚ö†Ô∏è **Aviso**: Apesar de existir a op√ß√£o de fazermos a execu√ß√£o usando `mix run`, isso pode causar problemas inesperados com outras partes da execu√ß√£o do projeto (como testes, compila√ß√£o ou Servers), pois o `mix run` por padr√£o executa um script, ou, quando voc√™ define uma application com `mod: {Main, []}`, em que ele chama `start/2` e se a fun√ß√£o `start/2` n√£o estiver bem configurada, podemos gerar problemas de dif√≠cil depura√ß√£o, por isso n√£o iremos abordar esse m√©todo aqui. Mas se quiser verificar, voc√™ pode checar na documenta√ß√£o do [start/2 no m√≥dulo Application](https://hexdocs.pm/elixir/Application.html#c:start/2), na documenta√ß√£o do Elixir.

### Usando Aliases

Para fazermos a execu√ß√£o usando alias no comando `mix`, vamos criar um novo alias no projeto. Para isso, configure o `mix.exs` da raiz:

**mix.exs:**

```elixir
defp aliases do
  [
    conversor_main: "run -e 'Main.main'"
  ]
end
```

Ser√° necess√°rio colocar tamb√©m a linha `aliases: aliases()` no `project` do `mix.exs` da raiz. Feito isso, √© s√≥ rodar o comando que o c√≥digo interativo de Main ser√° executado.

```bash
mix conversor_main
```

Esse comando √© o equivalente a rodarmos, no terminal:

```bash
mix run -e 'Main.main'
```

### Usando Mix.Tasks

Aqui vamos criar um novo arquivo em `apps/main/lib/mix/tasks/conversor_task.ex` com o seguinte conte√∫do:

**apps/main/lib/mix/tasks/conversor_task.ex:**

```elixir
defmodule Mix.Tasks.ConversorTask do
  use Mix.Task

  @shortdoc "Executa a aplica√ß√£o interativa do conversor"
  def run(_args) do
    # Garante que os apps do projeto est√£o carregados
    Mix.Task.run("app.start")

    # Chama a fun√ß√£o interativa
    Main.main()
  end
end
```

Criada a task, conseguiremos rodar nossa aplica√ß√£o com a task nova criada.

```bash
mix conversor_task
```

> üí° Para simplificar um pouco, usamos uma nomenclatura que nos permite ver quais os comandos que criamos, caso haja d√∫vidas. Ao rodar `mix help | grep conversor` iremos ver s√≥ os comandos que n√≥s criamos.

## Usando IEx com projetos umbrella

Com o projeto estruturado, voc√™ pode iniciar um console interativo a partir da raiz do projeto umbrella com:

```bash
iex -S mix
```

Isso compila e carrega todas as aplica√ß√µes definidas em `apps/`. Feito isso, voc√™ pode interagir com qualquer m√≥dulo dentro do projeto:

```elixir
iex> Conversor.Temperatura.f_para_c(212)
100.0
iex> Conversor.Distancia.m_para_ft(10)
32.8084
```

## Testes

Vamos adicionar testes automatizados para os nossos m√≥dulos. Cada m√≥dulo possui seus testes, organizados em arquivos separados com `ExUnit`. Usaremos valores aleat√≥rios gerados pela seed do ExUnit, garantindo reprodutibilidade com o comando `mix test --seed <valor>`. Isso √© √∫til para capturar inconsist√™ncias sutis em c√°lculos com ponto flutuante.

### Testes para `Conversor.Distancia`

**apps/conversor_distancia/test/conversor_distancia_test.exs:**

```elixir
defmodule Conversor.DistanciaTest do
  use ExUnit.Case, async: true
  doctest Conversor.Distancia

  describe "Teste do Conversor de Dist√¢ncias" do
    test "m_para_ft/1 com inteiro aleat√≥rio" do
      valor = :rand.uniform(100)
      esperado = valor * 3.28084
      assert_in_delta Conversor.Distancia.m_para_ft(valor), esperado, 0.0001
    end

    test "ft_para_m/1 com inteiro aleat√≥rio" do
      valor = :rand.uniform(100)
      esperado = valor / 3.28084
      assert_in_delta Conversor.Distancia.ft_para_m(valor), esperado, 0.0001
    end
  end
end
```

### Testes para `Conversor.Temperatura`

**apps/conversor_temperatura/test/conversor_temperatura_test.exs:**

```elixir
defmodule Conversor.TemperaturaTest do
  use ExUnit.Case, async: true
  doctest Conversor.Temperatura

  describe "Teste do Conversor de Temperaturas" do
    test "c_para_f/1 com inteiro aleat√≥rio" do
      valor = :rand.uniform(100) - 50
      esperado = (valor * 1.8) + 32
      assert_in_delta Conversor.Temperatura.c_para_f(valor), esperado, 0.0001
    end

    test "f_para_c/1 com inteiro aleat√≥rio" do
      valor = :rand.uniform(212)
      esperado = (valor - 32) / 1.8
      assert_in_delta Conversor.Temperatura.f_para_c(valor), esperado, 0.0001
    end
  end
end

```

### Teste para a chamada interativa de `Main`

**apps/main/test/main_test.exs:**

```elixir
defmodule MainTest do
  use ExUnit.Case
  import ExUnit.CaptureIO

  test "main/0 realiza intera√ß√£o com o usu√°rio" do
    output =
      capture_io("100\n10\n", fn ->
        Main.main()
      end)

    assert output =~ "Digite uma temperatura em Celsius:"
    assert output =~ "Em Fahrenheit: 212.0"
    assert output =~ "Digite uma dist√¢ncia em metros:"
    assert output =~ "Em p√©s: 32.8084"
  end
end
```

Para executar todos os testes, √© s√≥ rodar na raiz:

```bash
mix test
```

Se quiser ver os testes de forma completa:

```bash
mix test --trace
```

Ou testar apenas um subprojeto:

```bash
cd apps/conversor_distancia
mix test
```

A separa√ß√£o por m√≥dulos facilita testes unit√°rios isolados e estimula boas pr√°ticas.

## Conclus√£o

Neste post, criamos um projeto guard-chuva com tr√™s m√≥dulos Elixir. Com ele, aprendemos a:

- Organizar um sistema modular e escal√°vel
- Integrar m√∫ltiplos m√≥dulos com depend√™ncias locais
- Utilizar `iex`, `mix test`, `mix compile` e `mix run` de forma produtiva

Al√©m disso, refor√ßamos a id√©ia de que **m√≥dulos no Elixir** s√£o unidades organizacionais que permitem estruturar bem o c√≥digo. Ao usar nomes como `Conversor.Temperatura` e `Conversor.Distancia`, deixamos clara a inten√ß√£o e responsabilidade de cada parte do sistema.

Essa estrutura favorece a clareza, testes isolados e evolu√ß√£o cont√≠nua de sistemas mais robustos.

## Refer√™ncias üîó

- [Documenta√ß√£o oficial do Elixir](https://elixir-lang.org/)
- [Guia oficial do Mix](https://hexdocs.pm/mix/Mix.html)
- [Umbrella Projects no Elixir School](https://elixirschool.com/en/lessons/advanced/umbrella_projects/)
- [Testing no Elixir School](https://elixirschool.com/en/lessons/testing/basics)
- [Custom Mix Tasks no Elixir School](https://elixirschool.com/en/lessons/intermediate/mix_tasks)

